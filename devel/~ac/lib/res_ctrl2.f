( Контроль расходования ресурсов ОС форт-системой )
( Andrey Cherezov, 02.05.2001, 23.03.2007 )

REQUIRE CREATE-MUTEX lib/win/mutex.f 

WINAPI: GetCurrentThreadId KERNEL32.DLL
100000 VALUE RSIZE
VARIABLE RES-MUT
0 VALUE RTABLE

0
CELL -- R_TYPE
CELL -- R_ID
CELL -- R_THREAD
CELL -- R_FROM
CONSTANT /R_SIZE

CREATE RRES HERE /R_SIZE DUP ALLOT ERASE

: FIND-FREE-RES ( -- addr flag )
  RTABLE DUP RSIZE + SWAP DO
    I R_TYPE @ 0= IF I UNLOOP TRUE EXIT THEN
  /R_SIZE +LOOP HERE FALSE
;
: +RES ( handle type -- )
  -1 RES-MUT @ WAIT 2DROP
  FIND-FREE-RES 
  IF >R
    R@ R_TYPE ! R@ R_ID !
    RP@ CELL+ CELL+ @ R@ R_FROM !
    GetCurrentThreadId R> R_THREAD !
  ELSE DROP ." +RES ERROR" THEN
  RES-MUT @ RELEASE-MUTEX DROP
;
USER -RESF

: -RES ( handle type -- )
  -1 RES-MUT @ WAIT 2DROP
  FALSE -RESF !
  RTABLE DUP RSIZE + SWAP DO
    2DUP I R_TYPE @ = SWAP I R_ID @ = AND
\    I R_THREAD @ GetCurrentThreadId = AND
    IF I /R_SIZE ERASE TRUE -RESF ! LEAVE THEN
  /R_SIZE +LOOP
  -RESF @
  IF 2DROP
  ELSE
    CR ." -RES res not found:" COUNT TYPE .
    R> R@ WordByAddr TYPE >R CR 
  THEN
  RES-MUT @ RELEASE-MUTEX DROP
;
: INIT-RTABLE
  S" ESERV3_RES" FALSE CREATE-MUTEX DROP RES-MUT !
  RSIZE ALLOCATE THROW TO RTABLE
  RTABLE RSIZE ERASE
;
: DUMP-RES ( -- )
  -1 RES-MUT @ WAIT 2DROP
  RTABLE DUP RSIZE + SWAP DO
    I R_TYPE @ ?DUP
    IF COUNT TYPE SPACE I R_ID @ . I R_THREAD @ . I R_FROM @ WordByAddr TYPE CR THEN
  /R_SIZE +LOOP
  RES-MUT @ RELEASE-MUTEX DROP
  ." -----------------------------------" CR
;

CREATE FILE_RES C" FILE_RES" ",

: OPEN-FILE
  OPEN-FILE DUP 0= IF OVER FILE_RES +RES THEN
;
: OPEN-FILE-SHARED
  OPEN-FILE-SHARED DUP 0= IF OVER FILE_RES +RES THEN
;
: CREATE-FILE
  CREATE-FILE DUP 0= IF OVER FILE_RES +RES THEN
;
: CREATE-FILE-SHARED
  CREATE-FILE-SHARED DUP 0= IF OVER FILE_RES +RES THEN
;
: CLOSE-FILE
  DUP IF DUP FILE_RES -RES THEN
  CLOSE-FILE
;
: INCLUDE-FILE
  DUP >R INCLUDE-FILE
  R> FILE_RES -RES
;

\EOF
INIT-RTABLE
S" test111" R/W CREATE-FILE . CR
S" test112" R/W CREATE-FILE . CR
DUMP-RES
CLOSE-FILE . CR
DUMP-RES
DUP CLOSE-FILE . CR
CLOSE-FILE . CR \ ошибка 6
DUMP-RES
