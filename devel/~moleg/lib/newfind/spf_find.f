\ 11-11-2006 ~mOleg
\ Copyright [C] 2006-2007 mOleg mininoleg@yahoo.com
\ мое предложение для замены стандартного СПФовского sfind

 REQUIRE ?DEFINED devel\~moleg\lib\util\ifdef.f

FALSE WARNING !

\ на во-первых, написанный на форте, а во вторых
\            немного иначе разбитый Sfind. оригинальный текст взят
\            из версии 4.17

\ на случай смены фомата словарной статьи
: ID>ASC COUNT ;

\ стоит выделить процедуру сравнения идентификатора слова
\ с ключевой строкой в отедельное слово
: identify ( asc # nameid --> flag )
           ID>ASC 2SWAP COMPARE ;

\ Найти определение, заданное строкой c-addr u в списке слов, идентифицируемом
\ wid. Если определение не найдено, вернуть ноль.
\ Если определение найдено, вернуть выполнимый токен xt и единицу (1), если
\ определение немедленного исполнения, иначе минус единицу (-1).
: SEARCH-WORDLIST1 ( c-addr u wid -- 0 | xt 1 | xt -1 )
      @ BEGIN DUP WHILE
               >R 2DUP R@ identify
                  WHILE
                  R>
              CDR
         REPEAT
           2DROP
           R> DUP NAME>C @
              SWAP ?IMMEDIATE IF 1 ELSE -1 THEN  \ некрасиво, но иначе никак
         EXIT                                    \ хотя можно этот код перенести
        THEN NIP NIP ;                           \ в ?IMMEDIATE

\ 2006-12-09
' SEARCH-WORDLIST1 TO SEARCH-WORDLIST

\ найти исполнимый адрес слова, идентифицируемого строкой asc #
\ в списке словарей widn .. wid1. Список словарей снизу ограничивается нулем
: sfindin ( 0 widn .. wid1 asc # --> addr u 0 | xt 1 | xt -1 )
          2>R

          BEGIN DUP WHILE
                2R@ ROT SEARCH-WORDLIST
                        DUP 0= WHILE
                DROP
           REPEAT
              BEGIN ROT WHILE REPEAT
              RDROP RDROP EXIT
          THEN

          2R> ROT ;


\ Расширить семантику CORE FIND следующим:
\ Искать определение с именем, заданным строкой addr u.
\ Если определение не найдено после просмотра всех списков в порядке поиска,
\ возвратить addr u и ноль. Если определение найдено, возвратить xt.
\ Если определение немедленного исполнения, вернуть также единицу (1);
\ иначе также вернуть минус единицу (-1). Для данной строки, значения,
\ возвращаемые FIND во время компиляции, могут отличаться от значений,
\ возвращаемых не в режиме компиляции.
: SFIND ( addr u -- addr u 0 | xt 1 | xt -1 ) \ 94 SEARCH
        2>R 0 GET-ORDER DROP
        2R> sfindin ;

: LATEST ( -> NFA ) GET-CURRENT @ ;

\ Расширить семантику CORE FIND следующим:
\ Искать определение с именем, заданным строкой со счетчиком c-addr.
\ Если определение не найдено после просмотра всех списков в порядке поиска,
\ возвратить c-addr и ноль. Если определение найдено, возвратить xt.
\ Если определение немедленного исполнения, вернуть также единицу (1);
\ иначе также вернуть минус единицу (-1). Для данной строки, значения,
\ возвращаемые FIND во время компиляции, могут отличаться от значений,
\ возвращаемых не в режиме компиляции.
: FIND1 ( c-addr -- c-addr 0 | xt 1 | xt -1 ) \ 94 SEARCH
        DUP >R COUNT SFIND
        DUP 0= IF NIP NIP R> SWAP ELSE RDROP THEN ;

TRUE WARNING !

?DEFINED test{ \EOF -- тестовая секция ---------------------------------------

test{ \ просто проверяем собирабельность кода
  S" passed" TYPE
}test
