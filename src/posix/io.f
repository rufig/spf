\ $Id$
\ 
\ Файловый ввод-вывод
\ Ю. Жиловец, 9.05.2007

: CLOSE-FILE ( fileid -- ior ) \ 94 FILE
\ Закрыть файл, заданный fileid.
\ ior - определенный реализацией код результата ввода/вывода.
  1 <( )) close ?ERR NIP
;

: CREATE-FILE ( c-addr u fam -- fileid ior ) \ 94 FILE
\ Создать файл с именем, заданным c-addr u, и открыть его в соответствии
\ с методом доступа fam. Смысл значения fam определен реализацией.
\ Если файл с таким именем уже существует, создать его заново как
\ пустой файл.
\ Если файл был успешно создан и открыт, ior нуль, fileid его идентификатор,
\ и указатель чтения/записи установлен на начало файла.
\ Иначе ior - определенный реализацией код результата ввода/вывода,
\ и fileid неопределен.
  NIP 
  \ для совместимости с SPF/Win, если маска < 3 (т.е. на деле передано
  \ W/O, R/O, R/W), устанавливаем маску u+rw
  DUP 3 < IF DROP 0x180 THEN
  2 <( )) creat64 ?ERR
;

: DELETE-FILE ( c-addr u -- ior ) \ 94 FILE
\ Удалить файл с именем, заданным строкой c-addr u.
\ ior - определенный реализацией код результата ввода/вывода.
  DROP 1 <( )) unlink ?ERR NIP
;

: FILE-POSITION ( fileid -- ud ior ) \ 94 FILE
\ ud - текущая позиция в файле, идентифицируемом fileid.
\ ior - определенный реализацией код результата ввода/вывода.
\ ud неопределен, если ior не ноль.
  1 <( 0. SEEK_CUR __ret2 )) lseek64
  2DUP -1. D= IF errno ELSE 0 THEN
;

: OPEN-FILE ( c-addr u fam -- fileid ior ) \ 94 FILE
\ Открыть файл с именем, заданным строкой c-addr u, с методом доступа fam.
\ Смысл значения fam определен реализацией.
\ Если файл успешно открыт, ior ноль, fileid его идентификатор, и файл
\ позиционирован на начало.
\ Иначе ior - определенный реализацией код результата ввода/вывода,
\ и fileid неопределен.
  NIP 2 <( )) open64 ?ERR
;

\ В Линуксе файлы по умолчанию не блокируются.
\ Слова определены для совместимости с SPF/Win

: CREATE-FILE-SHARED ( c-addr u fam -- fileid ior )
  CREATE-FILE
;
: OPEN-FILE-SHARED ( c-addr u fam -- fileid ior )
  OPEN-FILE
;

: READ-FILE ( c-addr u1 fileid -- u2 ior ) \ 94 FILE
\ Прочесть u1 символов в c-addr из текущей позиции файла,
\ идентифицируемого fileid.
\ Если u1 символов прочитано без исключений, ior ноль и u2 равен u1.
\ Если конец файла достигнут до прочтения u1 символов, ior ноль
\ и u2 - количество реально прочитанных символов.
\ Если операция производится когда значение, возвращаемое
\ FILE-POSITION равно значению, возвращаемому FILE-SIZE для файла
\ идентифицируемого fileid, ior и u2 нули.
\ Если возникла исключительная ситуация, то ior - определенный реализацией
\ код результата ввода/вывода, и u2 - количество нормально переданных в
\ c-addr символов.
\ Неопределенная ситуация возникает, если операция выполняется, когда
\ значение, возвращаемое FILE-POSITION больше чем значение, возвращаемое
\ FILE-SIZE для файла, идентифицируемого fileid, или требуемая операция
\ пытается прочесть незаписанную часть файла.
\ После завершения операции FILE-POSITION возвратит следующую позицию
\ в файле после последнего прочитанного символа.
  -ROT 3 <( )) read ?ERR
;

: REPOSITION-FILE ( ud fileid -- ior ) \ 94 FILE
\ Перепозиционировать файл, идентифицируемый fileid, на ud.
\ ior - определенный реализацией код результата ввода-вывода.
\ Неопределенная ситуация возникает, если позиционируется вне
\ его границ.
\ После завершения операции FILE-POSITION возвращает значение ud.
  -ROT 3 <( SEEK_SET __ret2 )) lseek64
  -1. D= IF errno ELSE 0 THEN
;


USER _fp1
USER _fp2
USER _addr

: READ-LINE ( c-addr u1 fileid -- u2 flag ior ) \ 94 FILE
\ Прочесть следующую строку из файла, заданного fileid, в память
\ по адресу c-addr. Читается не больше u1 символов. До двух
\ определенных реализацией символов "конец строки" могут быть
\ прочитаны в память за концом строки, но не включены в счетчик u2.
\ Буфер строки c-addr должен иметь размер как минимум u1+2 символа.
\ Если операция успешна, flag "истина" и ior ноль. Если конец строки
\ получен до того как прочитаны u1 символов, то u2 - число реально
\ прочитанных символов (0<=u2<=u1), не считая символов "конец строки".
\ Когда u1=u2 конец строки уже получен.
\ Если операция производится, когда значение, возвращаемое
\ FILE-POSITION равно значению, возвращаемому FILE-SIZE для файла,
\ идентифицируемого fileid, flag "ложь", ior ноль, и u2 ноль.
\ Если ior не ноль, то произошла исключительная ситуация и ior -
\ определенный реализацией код результата ввода-вывода.
\ Неопределенная ситуация возникает, если операция выполняется, когда
\ значение, возвращаемое FILE-POSITION больше чем значение, возвращаемое
\ FILE-SIZE для файла, идентифицируемого fileid, или требуемая операция
\ пытается прочесть незаписанную часть файла.
\ После завершения операции FILE-POSITION возвратит следующую позицию
\ в файле после последнего прочитанного символа.
  DUP >R
  FILE-POSITION IF 2DROP 0 0 THEN _fp1 ! _fp2 !
  LTL @ +
  OVER _addr !

  R@ READ-FILE ?DUP IF NIP RDROP 0 0 ROT EXIT THEN

  DUP >R 0= IF RDROP RDROP 0 0 0 EXIT THEN \ были в конце файла

  _addr @ R@ EOLN SEARCH
  IF   \ найден разделитель строк
     DROP _addr @ -
     DUP
     LTL @ + S>D _fp2 @ _fp1 @ D+ RDROP R> REPOSITION-FILE DROP
  ELSE \ не найден разделитель строк
     2DROP
     R> RDROP  \ если строка прочитана не полностью - будет разрезана
  THEN
  TRUE 0
;

: WRITE-FILE ( c-addr u fileid -- ior ) \ 94 FILE
\ Записать u символов из c-addr в файл, идентифицируемый fileid,
\ в текущую позицию.
\ ior - определенный реализацией код результата ввода-вывода.
\ После завершения операции FILE-POSITION возвращает следующую
\ позицию в файле за последним записанным в файл символом, и
\ FILE-SIZE возвращает значение большее или равное значению,
\ возвращаемому FILE-POSITION.
  -ROT DUP >R
  3 write-adr @ C-CALL 
  DUP -1 = IF 
    R> 2DROP errno
  ELSE
    R> <>
    ( если записалось не столько, сколько требовалось, то тоже ошибка )
  THEN
;

: RESIZE-FILE ( ud fileid -- ior ) \ 94 FILE
\ Установить размер файла, идентифицируемого fileid, равным ud.
\ ior - определенный реализацией код результата ввода-вывода.
\ Если результирующий файл становится больше, чем до операции,
\ часть файла, добавляемая в результате операции, может быть
\ не записана.
\ После завершения операции FILE-SIZE возвращает значение ud
\ и FILE-POSITION возвращает неопределенное значение.
  -ROT 3 <( )) ftruncate64 ?ERR NIP
;

: WRITE-LINE ( c-addr u fileid -- ior ) \ 94 FILE
\ Записать u символов от c-addr с последующим зависящим от реализации концом 
\ строки в файл, идентифицируемый fileid, начиная с текущей позиции.
\ ior - определенный реализацией код результата ввода-вывода.
\ После завершения операции FILE-POSITION возвращает следующую
\ позицию в файле за последним записанным в файл символом, и
\ FILE-SIZE возвращает значение большее или равное значению,
\ возвращаемому FILE-POSITION.
  DUP >R WRITE-FILE ?DUP IF RDROP EXIT THEN
  EOLN R> WRITE-FILE
;

: FLUSH-FILE ( fileid -- ior ) \ 94 FILE EXT
  1 <( )) fsync ?ERR NIP
;

USER-CREATE API-BUFFER
200 TC-USER-ALLOT

\ TRUE если существует путь addr u
: FILE-EXIST ( addr u -- f )
  DROP >R (( 3 R> API-BUFFER )) __xstat 0=
;

: FILE-EXISTS ( addr u -- f )
  FILE-EXIST 0 = IF FALSE EXIT THEN
  API-BUFFER SYS_STAT_ST_MODE + @ SYS_S_IFMT AND SYS_S_IFREG =
;

: FILE-SIZE ( fileid -- ud ior ) \ 94 FILE
\ ud - размер в символах файла, идентифицируемом fileid.
\ ior - определенный реализацией код результата ввода/вывода.
\ Эта операция не влияет на значение, возвращаемое FILE-POSITION.
\ ud неопределен, если ior не ноль.
  >R (( 1 R> API-BUFFER )) __fxstat64
  -1 = IF 0. errno ELSE API-BUFFER 11 CELLS + 2@ SWAP 0 THEN
;
